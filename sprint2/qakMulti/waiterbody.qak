System waiterbody

//MQTT
mqttBroker "localhost": 1883 eventTopic "unibo/polar"

//MESSAGES
//for walker
Request moveToEntrance : moveToEntrance(ARG) 
Request moveToTable : moveToTable(TABLE) 
Request moveToBarman : moveToBarman(ARG) 
Request moveToExit : moveToExit(ARG) 
Request moveToHome : moveToHome(ARG) 

Reply moveOk : moveOk(ARG)

//tearoom management
Request tableToClean: tableToClean(OK) 
Reply dirtyTable: dirtyTable(TABLE) 

Dispatch startTimer: startTimer(OK) 
Dispatch stopTime: stopTimer(OK) 

Request makeTea : makeTea( TEA, TABLE ) 
Reply teaReady : teaReady( TEA , TABLE) 

Dispatch readyToPay: readyToPay(TABLE) 
Request readyToOrder: readyToOrder(TABLE) 
Reply   atTable: atTable(TABLE) 
Dispatch moneyCollected: moneyCollected(AMOUNT) 

Dispatch newClient: newClient(ID) 

//for tearoom.qak
Request tableToClean: tableToClean(OK)
Reply dirtyTable: dirtyTable(TABLE)

Request updateDirty: updateDirty(TABLE)
Reply dirtyUpdated: dirtyUpdated(OK)

Request updateClean: updateClean(TABLE)
Reply cleanUpdated: cleanUpdated(OK)

Request updateTaken: updateTaken(OK)
Reply takenUpdated: takenUpdated(TABLE)

Request checkTableState: checkTableState(TABLE) 
Reply tableState: tableState(OK) 

//order
Dispatch order: order(TEA, TABLE)

//CONTEXT
Context ctxwaitermind ip [host="localhost" port=8020]
Context ctxwaiterbody ip [host="localhost" port=8021]
Context ctxtearoom ip [host="localhost" port=8040]
Context ctxwalker ip [host="localhost" port=8022]
Context ctxbarman ip [host="localhost" port=8030]


//EXTERNAL
ExternalQActor tearoom context ctxtearoom
ExternalQActor walker context ctxwalker
ExternalQActor barman context ctxbarman

//ACTOR
QActor waiterbody context ctxwaiterbody{
	
	[#
		var GoHomeTime = 5000L
		
		var TABLE = 0
		var TEA = 0
		
		var StateTable = ""
		
	#]
	
	State s0 initial{
		discardMsg Off
	}Goto home
	
	State home{
		updateResource[# "home" #]
		println("waiterbody | home")
		
		request tearoom -m tableToClean: tableToClean(OK)
		delay 500
		
	}Transition t0 whenReply dirtyTable -> goToClean
	 			   whenMsg newClient -> goToEntrance
	 			   whenRequest readyToOrder -> goToTakeOrder
   				   whenReply teaReady -> takeTea
				   whenMsg readyToPay -> checkTableState
				  		   
   	State wait{
   	   	updateResource[# "wait" #]
   		println("waiterbody | wait")
		
		request tearoom -m tableToClean: tableToClean(OK)
		delay 500
   		
	}Transition t0 whenTimeVar GoHomeTime -> goToHome
				   whenReply dirtyTable -> goToClean
	 			   whenMsg newClient -> goToEntrance
	 			   whenRequest readyToOrder -> goToTakeOrder
   				   whenReply teaReady -> takeTea
				   whenMsg readyToPay -> checkTableState
   
   State goToEntrance{
   		updateResource[# "goToEntrance" #]
   		println("waiterbody | goToEntrance")
   		request walker -m moveToEntrance: moveToEntrance(OK)
   }Transition t0 whenReply moveOk -> bookTable
   
   State bookTable{
   		updateResource[# "bookTable" #]
   		println("waiterbody | bookTable")

		request tearoom -m updateTaken: updateTaken(OK)
   		
   }Transition t0 whenReply takenUpdated -> convoyToTable
   
   State convoyToTable{
   		println("waiterbody | convoyToTable")
   		
   		onMsg( takenUpdated: takenUpdated(TABLE)){
   			[# 
   				TABLE = payloadArg(0).toString().toInt() 
			#]
			request walker -m moveToTable: moveToTable($TABLE)
		}
		
		updateResource[# "convoyToTable " + TABLE #]
   	
   }Transition t0 whenReply moveOk -> wait
   
   State goToClean{
   		println("waiterbody | goToClean")
		onMsg(dirtyTable : dirtyTable(TABLE)){

   			[# 
   				TABLE = payloadArg(0).toString().toInt() 
			#]
			request walker -m moveToTable: moveToTable($TABLE)
			
		}
		
		updateResource[# "goToClean " + TABLE #]
   }Transition t0 whenReply moveOk -> clean
   
   State clean{
   		updateResource[# "clean " + TABLE #]
   		println("waiterbody | clean")
		request tearoom -m updateClean: updateClean($TABLE)

   } Transition t0 whenReply cleanUpdated -> wait
   
   State checkTableState{
   		println("waiterbody | checkTableState")
		onMsg(readyToPay : readyToPay(TABLE)){
		
		 [#
		 	TABLE = payloadArg(0).toString().toInt() 
		 #]
		}
		
		 if [# TABLE == 1 #] {
		 	forward timer1 -m stopTime: stopTime(now)
		 }
		 else{
		 	forward timer2 -m stopTime: stopTime(now)
		 }
		 
		request tearoom -m checkTableState: checkTableState($TABLE)
   }Transition t0 whenReply tableState -> checkState
   
   State checkState{
   		println("waiterbody | checkState")
		onMsg(tableState : tableState(STATE)){
		 [#
		 	StateTable = payloadArg(0).toString()
		 #]
		}
   }Goto goToCollect if [# StateTable == "taken" #] else wait
   
   State goToCollect{
   		println("waiterbody | goToCollect")

		request walker -m moveToTable: moveToTable($TABLE)
		updateResource[# "goToCollect " + TABLE #]
   } Transition t0 whenReply moveOk -> collect
   
   State collect{
   	   	updateResource[# "collect " + TABLE #]
   		println("waiterbody | collect")

   }Transition t0 whenMsg moneyCollected -> goToExit
   
   State goToExit{
   	   	updateResource[# "goToExit" #]
   		println("waiterbody | goToExit")
   		
   		request tearoom -m updateDirty: updateDirty($TABLE)
   		request walker -m moveToExit: moveToExit(OK)
   		
   }Transition t0 whenReply moveOk -> wait
   
   State goToTakeOrder{
   		println("waiterbody | goToTakeOrder")
		onMsg(readyToOrder : readyToOrder(TABLE)){
 			 [#
			 	TABLE = payloadArg(0).toString().toInt() 
			 #]
			 request walker -m moveToTable: moveToTable($TABLE)
		}
   	   	updateResource[# "goToTakeOrder " + TABLE #]
   } Transition t0 whenReply moveOk -> takeOrder
   
   State takeOrder{
   	   	updateResource[# "takeOrder " + TABLE #]
   		println("waiterbody | takeOrder")
   		replyTo readyToOrder with atTable: atTable
		
   }Transition t0 whenMsg order -> sendOrder 
	
   State sendOrder{
   	   	updateResource[# "sendOrder" #]
   		println("waiterbody | sendOrder")
   		println("waiterbody | sending the order to the barman")
		onMsg(order : order(TEA, TABLE)){
			 [#
			 	TEA  = payloadArg(0).toString().toInt() 
			 	TABLE = payloadArg(1).toString().toInt() 
			 #]
			 request barman -m makeTea: makeTea($TEA, $TABLE)
		}
 		
	}Goto wait
	
   State takeTea{
   	   	updateResource[# "takeTea" #]
   		println("waiterbody | takeTea")
   		println("waiterbody | going to the barman to take the tea")
		onMsg(teaReady : teaReady(TEA, TABLE)){
			 [#
			 	TEA  = payloadArg(0).toString().toInt() 
			 	TABLE = payloadArg(1).toString().toInt() 
			 #]
		 
		 }
   		request walker -m moveToBarman: moveToBarman(OK)
		
	}Transition t0 whenReply moveOk -> serve
	
   State serve{
   	   	updateResource[# "serve " + TABLE #]
   		println("waiterbody | serve")
   		println("waiterbody | going to serve the tea")
   		
   		request walker -m moveToTable: moveToTable($TABLE)
   		
	}Transition t0 whenReply moveOk -> startTimer
		
   State startTimer{
   		println("waiterbody | startTimer")
   		if [# TABLE == 1 #]{
   			forward timer1 -m startTimer: startTimer(OK)
   		}
   		else{
   			forward timer2 -m startTimer: startTimer(OK)
   		}
   }Goto wait
   			   
   State goToHome{
   	   	updateResource[# "goToHome" #]
   		println("waiterbody | goToHome")
   		println("waiterbody | going to home")
   		
    	request walker -m moveToHome: moveToHome(HOME)
    }Transition t0 whenReply moveOk -> home
}

QActor timer1 context ctxwaiterbody{
	[# var Timer = 30000L #]
	State s0 initial{
		discardMsg On	
	}Goto wait
	
	State wait{
		println("timer1 | wait")	
	}Transition t0 whenMsg startTimer -> timer
	
	State timer{
		println("timer1 | timer")	
	}Transition t0 whenTimeVar Timer -> timeover
				   whenMsg stopTime -> wait
    
    State timeover{
    	println("timer1 | timerover")
    	forward waiterbody -m readyToPay: readyToPay(1)
    }Goto wait
}

QActor timer2 context ctxwaiterbody{
	[# var Timer = 30000L #]
	State s0 initial{
		discardMsg On	
	}Goto wait
	
	State wait{
		println("timer2 | wait")	
	}Transition t0 whenMsg startTimer -> timer
	
	State timer{
		println("timer2 | timer")	
	}Transition t0 whenTimeVar Timer -> timeover
				   whenMsg stopTime -> wait
    
    State timeover{
    	println("timer2 | timerover")
    	forward waiterbody -m readyToPay: readyToPay(2)
    }Goto wait
}